group 'leocdp'

//-------------------------------------------------------------------------------
// load ext properties from the file leocdp-build.properties 
file("./leocdp-build.properties").withReader { 
   Properties props = new Properties()
   props.load(it)
   project.ext.leoCdpBuild = props
}

// the output for BUILD 
// Read property from command line, or fallback to default
def buildOutputFolderPath = project.hasProperty("buildOutputFolderPath") 
    ? project.getProperty("buildOutputFolderPath").trim() 
    :  leoCdpBuild['buildOutputFolderPath'].trim()

// the output for STATIC files in CDN
def staticOutputFolderPath = project.hasProperty("staticOutputFolderPath") 
    ? project.getProperty("staticOutputFolderPath").trim() 
    :  leoCdpBuild['staticOutputFolderPath'].trim()


//-------------------------------------------------------------------------------

// load ext properties from the file leocdp-metadata.properties 
file("./leocdp-metadata.properties").withReader { 
   Properties props = new Properties()
   props.load(it)
   project.ext.leoCdpMetadata = props
}

//////////////////////////////////////////
version leoCdpMetadata['buildVersion'] ///
//////////////////////////////////////////

buildscript {
	repositories {
		mavenCentral()
    	maven {
      		url "https://plugins.gradle.org/m2/"
    	}
    	mavenLocal()
  	}
  	dependencies {
    	classpath "org.gradle-webtools.minify:gradle-minify-plugin:1.3.2"
  	}
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: "org.gradlewebtools.minify"

eclipse {
    project {
        name = 'LeoTech-Core-System'
    }
}

sourceCompatibility = 11 

repositories {
 	mavenLocal()
	google()
    mavenCentral()
    maven {
    	url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

configurations.all {
   exclude module: 'ch.qos.logback'
}

dependencies {
	//  ────────────────────── Local Java Libs: rfx ────────────────────── l
	implementation(
        fileTree(dir: 'ext-lib', include: '**/*.jar')
    )

    // ────────────────────── Date & Time ──────────────────────
    implementation 'joda-time:joda-time:2.12.2'

    // ────────────────────── JSON (Jackson) ──────────────────────
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.2'
    implementation 'com.fasterxml.jackson.module:jackson-module-scala_2.13:2.14.2'

    // ────────────────────── CSV / TSV Parser ──────────────────────
    implementation 'com.univocity:univocity-parsers:2.9.1'

    // ────────────────────── Expression Language ──────────────────────
    implementation 'org.mvel:mvel2:2.5.2.Final'

    // ────────────────────── Database Drivers ──────────────────────
    implementation 'com.mysql:mysql-connector-j:9.5.0'
    implementation 'org.postgresql:postgresql:42.7.8'
    implementation 'org.xerial:sqlite-jdbc:3.50.3.0'

    // ────────────────────── Database Utility ──────────────────────
    implementation 'org.jdbi:jdbi3-core:3.49.6'
    implementation 'org.jdbi:jdbi3-sqlobject:3.49.6'

    // ────────────────────── Image & Captcha ──────────────────────
    implementation 'net.coobird:thumbnailator:0.4.20'
    implementation 'net.logicsquad:nanocaptcha:2.1'

    // ────────────────────── Phone Number Parser ──────────────────────
    implementation 'com.googlecode.libphonenumber:libphonenumber:9.0.17'

    // ────────────────────── Job Scheduling ──────────────────────
    implementation 'org.quartz-scheduler:quartz:2.3.2'

    // ────────────────────── Email & Templates ──────────────────────
    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'io.rocketbase.mail:email-template-builder:2.5.0'

    // ────────────────────── Google OAuth & APIs ──────────────────────
    implementation 'com.google.oauth-client:google-oauth-client:1.34.1'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
    implementation 'com.google.apis:google-api-services-drive:v3-rev20231128-2.0.0'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20230815-2.0.0'
    implementation 'com.google.cloud:google-cloud-storage:2.22.5'

    // ────────────────────── Excel Writer ──────────────────────
    implementation 'org.dhatim:fastexcel:0.19.0'

    // ────────────────────── Database (ArangoDB) ──────────────────────
    implementation 'com.arangodb:arangodb-java-driver:6.25.0'

    // ────────────────────── Vert.x Framework ──────────────────────
    implementation 'io.vertx:vertx-core:3.8.5'
    implementation 'io.vertx:vertx-web:3.8.5'
    implementation 'io.vertx:vertx-web-client:3.8.5'
    implementation 'io.vertx:vertx-codegen:3.8.5'
    implementation 'io.vertx:vertx-auth-jwt:3.8.5'

    // ────────────────────── JSON (Gson) ──────────────────────
    implementation 'com.google.code.gson:gson:2.9.1'

   	// ────────────────────── Logging (SLF4J + Log4j2) ──────────────────────
	implementation 'org.slf4j:slf4j-api:2.0.13'                         // SLF4J API
	implementation 'org.apache.logging.log4j:log4j-api:2.23.1'          // Log4j2 API
	implementation 'org.apache.logging.log4j:log4j-core:2.23.1'         // Log4j2 Engine
	implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'  // Bridge for SLF4J 2.x → Log4j2

    // ────────────────────── Security / XSS Filter ──────────────────────
    implementation 'org.jsoup:jsoup:1.21.1'
    implementation 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20240325.1'

    // ────────────────────── Common Utilities ──────────────────────
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'commons-net:commons-net:3.6'
    implementation 'commons-io:commons-io:2.5'
    implementation 'redis.clients:jedis:7.0.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'

    // ────────────────────── Performance & Templates ──────────────────────
    implementation 'org.databene:contiperf:2.3.4'
    implementation 'com.google.javascript:closure-compiler:v20210302'
    implementation 'com.github.jknack:handlebars:4.3.1'
    implementation 'com.github.spullara.mustache.java:compiler:0.9.6'
    implementation 'org.yaml:snakeyaml:1.33'

    // ────────────────────── ID & SEO Helpers ──────────────────────
    implementation 'com.devskiller.friendly-id:friendly-id:1.1.0'
    implementation 'com.github.slugify:slugify:2.5'
    implementation 'net.gcardone.junidecode:junidecode:0.4.1'

    // ────────────────────── Geo & Math ──────────────────────
    implementation 'com.maxmind.geoip2:geoip2:3.0.2'
    implementation 'com.google.openlocationcode:openlocationcode:1.0.4'
    implementation 'org.apache.commons:commons-math3:3.6.1'

    // ────────────────────── AdTech / HTTP / Servlet ──────────────────────
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'javax.servlet:javax.servlet-api:3.1.0'

    // ────────────────────── Kafka (Event Streaming) ──────────────────────
    implementation 'org.apache.kafka:kafka-clients:3.5.2'
    implementation 'org.apache.kafka:kafka-streams:3.5.2'

    // ────────────────────── Image Generation / QR Code ──────────────────────
    implementation 'org.docx4j.org.capaxit.textimage:TextImageGen:1.9'
    implementation 'com.github.jai-imageio:jai-imageio-core:1.4.0'
    implementation 'io.nayuki:qrcodegen:1.8.0'

    // ────────────────────── Testing ──────────────────────
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.3'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.22.0'
    testImplementation 'net.datafaker:datafaker:1.5.0'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Jar) {
	//destinationDirectory = file("$rootDir/BUILD-OUTPUT")
	destinationDirectory = file(buildOutputFolderPath)
}

// === Helper: generate build version string ===
def getImplementationVersion() {
    def date = new Date()
    def version = leoCdpMetadata['buildVersion'] ?: 'unknown'
    return "${version}_${date.format('yyyy-MM-dd-HH-mm-ss')}"
}


// === JS minification for common-resources in Web Admin ===
task minifyJsAdminResource(type: org.gradlewebtools.minify.JsMinifyTask) {
    srcDir = project.file("resources/app-templates/leocdp-admin/common-resources")
    dstDir = project.file("resources/app-templates/leocdp-admin/common-resources-min")

    options {
        originalFileNames = true
    }

    doLast {
        def codeVersion = getImplementationVersion()
        def comment = "// Version: ${codeVersion}\n"

        // loop all files in common-resources-min
        dstDir.eachFileRecurse { file ->
            if (file.name.endsWith(".js")) {
                def original = file.text
                file.text = comment + original
            }
        }

        println "✅ Minified JS files updated with version header: ${codeVersion}"
        println "   Source: ${project.relativePath(srcDir)}"
        println "   Output: ${project.relativePath(dstDir)}"
    }
}


// === JS minification for leo.observer.js only ===
task minifyJsLeoObserver(type: org.gradlewebtools.minify.JsMinifyTask) {
    // Source and destination directories (same folder)
    srcDir = project.file("public/js/leo-observer-src")
    dstDir = project.file("public/js/leo-observer")

    options {
        originalFileNames = false   // We will control output name manually
    }

    doLast {
        def codeVersion = getImplementationVersion()
        def comment = "// Version: ${codeVersion}\n"

        // loop all files in common-resources-min
        dstDir.eachFileRecurse { file ->
            if (file.name.endsWith(".js")) {
                def original = file.text
                file.text = comment + original
            }
        }

        println "✅ Minified JS files updated with version header: ${codeVersion}"
        println "   Source: ${project.relativePath(srcDir)}"
        println "   Output: ${project.relativePath(dstDir)}"
    }
}





// 
def getClasspathStringJars() {
    def baseFolder = 'deps/'
    def files = configurations.runtimeClasspath.files
    def separator = ' ; '

    // Map all runtime JARs to their relative paths under deps/
    def jarPaths = files.collect { "${baseFolder}${it.name}" }

    // Join them properly for the classpath string
    return "." + separator + jarPaths.join(separator)
}



/////////////////////////// BEGIN BUILD FOR DEPLOYED JAR ///////////////////////////////

task buildLeoMainHttpStarter(type: Jar) {	
	dependsOn classes   
    from(sourceSets.main.output) {
        include "**"
    }
    baseName = 'leo-main-starter'
    manifest {
   		attributes ('Implementation-Title': 'MainHttpStarter', 
        			'Implementation-Version': getImplementationVersion(),
        			'Main-Class': ' leotech.starter.MainHttpStarter',
        			'Class-Path' : getClasspathStringJars() 
        )
	}
	
	doLast {
  		println '###  buildLeoMainHttpStarter OK !!!  ###'
  	}
} 

task buildLeoDataObserverStarter(type: Jar) {	
	dependsOn classes   
    from(sourceSets.main.output) {
        include "**"
    }
    baseName = 'leo-observer-starter'
    manifest {
   		attributes ('Implementation-Title': 'DataObserverStarter', 
        			'Implementation-Version': getImplementationVersion(),
        			'Main-Class': ' leotech.starter.DataObserverStarter',
        			'Class-Path' : getClasspathStringJars() 
        )
	}
	
	doLast {
  		println '###  buildLeoDataObserverStarter OK !!!  ###'
  	}
}

task buildLeoScheduledJobStarter(type: Jar) {	
	dependsOn classes   
    from(sourceSets.main.output) {
        include "**"
    }
    baseName = 'leo-scheduler-starter'
    manifest {
   		attributes ('Implementation-Title': 'ScheduledJobStarter', 
        			'Implementation-Version': getImplementationVersion(),
        			'Main-Class': ' leotech.starter.ScheduledJobStarter',
        			'Class-Path' : getClasspathStringJars() 
        )
	}
	
	doLast {
  		println '###  buildLeoScheduledJobStarter OK !!!  ###'
  	}
}

task buildLeoDataProcessingStarter(type: Jar) {	
	dependsOn classes   
    from(sourceSets.main.output) {
        include "**"
    }
    baseName = 'leo-data-processing-starter'
    manifest {
   		attributes ('Implementation-Title': 'DataProcessingStarter', 
        			'Implementation-Version': getImplementationVersion(),
        			'Main-Class': ' leotech.starter.DataProcessingStarter',
        			'Class-Path' : getClasspathStringJars() 
        )
	}
	
	doLast {
  		println '###  buildLeoDataProcessingStarter OK !!!  ###'
  	}
}

task buildLeoUploadFileHttpStarter(type: Jar) {	
	dependsOn classes   
    from(sourceSets.main.output) {
        include "**"
    }
    baseName = 'leo-uploader-starter'
    manifest {
   		attributes ('Implementation-Title': 'UploadFileHttpStarter', 
        			'Implementation-Version': getImplementationVersion(),
        			'Main-Class': ' leotech.starter.UploadFileHttpStarter',
        			'Class-Path' : getClasspathStringJars() 
        			)
	}
	
	doLast {
  		println '###  buildLeoUploadFileHttpStarter OK !!!  ###'
  	}
}

/////////////////////////// BEGIN COPY TASKS ///////////////////////////////


// 1) copy configs
task copyConfigsFolderToBuild(type: Copy) {  
	from files('configs')
  	into buildOutputFolderPath + "/configs" 
  	exclude "**DEV**"
  	exclude "**PRO**"
  	
  	doLast {
  		println '###  copyConfigsFolderToBuild OK !!!  ###'
  	}
}



// 2) copy templates
task copyResourcesToBuild(type: Copy, dependsOn: minifyJsAdminResource) {  
	from files('resources')
  	into  buildOutputFolderPath + "/resources" 
  	
  	includeEmptyDirs = true
  	exclude "**/common-resources"
  	
  	doLast {
  		println '###  copyResourcesToBuild OK !!!  ###'
  	}
}

// 3) clean and copy dependency system libs
task copyRuntimeLibsToBuild(type: Copy) {
    def depsDir = file("$buildOutputFolderPath/deps")

    // first delete old jars
    doFirst {
        if (depsDir.exists()) {
            depsDir.deleteDir()
            println "### Deleted old jars in $depsDir ###"
        }
    }

    // then copy fresh jars
    from configurations.runtimeClasspath
    into depsDir

    doLast {
        println "### copyRuntimeLibsToBuild OK !!! ###"
    }
}


// 4) copy public folder to BUILD
task copyPublicFilesToBuild(type: Copy) {  
	from files('public')
  	into  buildOutputFolderPath + "/public"  
  	
  	includeEmptyDirs = true
  	
  	// excluded files
  	exclude "**/leo.proxy.js"
  	exclude "**/leo.observer.js"
  	exclude "**/leoads.src.js"
  	
  	// excluded folders
  	exclude "**/exported-files/**.*"
  	exclude "**/uploaded-files/**.*"
  	exclude "**/qrcode/**.png"
  	exclude "**/sample-data"
  	
  	doLast {
  		println '###  copyPublicFilesToBuild OK !!!  ###'
  	}
}

// 5) copy public folder to STATIC for public CDN at https://www.jsdelivr.com
// E.g: https://gcore.jsdelivr.net/gh/USPA-Technology/leo-cdp-static-files/js/leo-observer/leo.observer.min.js
task CopyPublicFolderToSTATIC(type: Copy) {  
	from files('public')
  	into staticOutputFolderPath  
  	
  	// excluded files
  	exclude "**/leo.proxy.js"
  	exclude "**/leo.observer.js"
  	exclude "**/leoads.src.js"
  	
  	// excluded folders
  	exclude "**/exported-files"
  	exclude "**/uploaded-files"
  	exclude "**/sample-data"
  	exclude "**/qrcode/**.png"
  	exclude "**/html/leo-event-proxy.html"
  	
  	doLast {
  		println '###  CopyPublicFolderToSTATIC OK !!!  ###'
  	}
}


// 6) copy shell-script-starter and installation
task CopyDevOpsScriptToBUILD(type: Copy) {  

    copy {
        from 'devops-script/docker-arangodb'
        into buildOutputFolderPath + '/devops-script/docker-arangodb'
    }

    copy {
        from 'devops-script/docker-kafka'
        into buildOutputFolderPath + '/devops-script/docker-kafka'
    }
    
    copy {
        from 'devops-script/kafka-docker-production'
        into buildOutputFolderPath + '/devops-script/kafka-docker-production'
    }

    copy {
        from 'devops-script/script-installation'
        into buildOutputFolderPath + '/devops-script/script-installation'
    }
    
    doLast {
    	println '###  CopyDevOpsScriptToBUILD OK !!!  ###'
    }
}

// 7) copy README.md, data flow image and release notes
task copyDocuments(type: Copy) {  

	from file('NOTES-FOR-NEW-SETUP.md')
  	into buildOutputFolderPath
  	
  	from file('ChangeLog.md')
  	into buildOutputFolderPath
  	
  	from file('ai-first-customer360.png')
  	into buildOutputFolderPath
  	
  	doLast {
  		println '###  copyDocuments OK !!!  ###'
  	}
}


// 8) Copy AQL code To Build
task CopyDbQueryTemplateToBuild(type: Copy) {  
  	
  	from file('resources/database/database-query-template.aql')
  	into buildOutputFolderPath + "/resources/database/"
  
	doLast {  
  		println '###  CopyDbQueryTemplateToBuild OK !!!  ###'
  	}
}

// 9) copy templates
task copyAirflowDagToBuild(type: Copy) {  
	from files('../airflow-dags')
  	into  buildOutputFolderPath + "/airflow-dags" 
  	
  	exclude "**/logs"
  	exclude "**/__pycache__"
  	
  	doLast {
  		println '###  copyAirflowDagToBuild OK !!!  ###'
  	}
}

// 10) compile and copy key Leo CDP jars
def mainJarTasks = [buildLeoMainHttpStarter, buildLeoDataObserverStarter, buildLeoScheduledJobStarter, buildLeoDataProcessingStarter]
task BuildAllJavaJars(dependsOn: mainJarTasks) {
	doLast {
    	println '###  BuildAllJavaJars OK !!!  ###'
    }
}

// 11) Build all system
def systemJarTasks = [ copyRuntimeLibsToBuild, copyConfigsFolderToBuild ]
def staticTasks = [CopyDevOpsScriptToBUILD, copyPublicFilesToBuild, copyResourcesToBuild ]
def allReleaseTasks = [minifyJsAdminResource, mainJarTasks, copyPublicFilesToBuild, copyResourcesToBuild, copyRuntimeLibsToBuild ]

task AutoBuildForDeployment(dependsOn: allReleaseTasks) {    
    doLast {
    	 println '###  AutoBuildForDeployment OK !!!  ###'
    }
}

defaultTasks 'AutoBuildForDeployment'

build {
    dependsOn AutoBuildForDeployment
}


/////////////////////////// END COPY TASKS ///////////////////////////////

/////////////////////////// END BUILD FOR DEPLOYED JAR ///////////////////////////////

// deploy jar to Remote Servers and restart all
task runDeployShellScript(type: Exec) {    
    commandLine 'sh', './shell-scripts/deploy-ad-server.sh'
}

// just restart all servers
task runRestartAllServers(type: Exec) {    
    commandLine 'sh', './shell-scripts/restart-all-ad-servers.sh'
}

